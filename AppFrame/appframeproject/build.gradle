apply plugin: 'com.android.application'
apply plugin: 'android-apt'
def AAVersion = '3.3.2'
def PRODUCT_NAME = "appframe"


def date = new Date()
def BUILD_TIME = date.format("yyyy-MM-dd HH:mm:ss")
def BUILD_TIME_FORMAT = new Date().format("yyyyMMdd")


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId "com.joyfulmath.appframe"
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 20160001
        versionName "1.0.1"
    }

    buildTypes {
        release {
            manifestPlaceholders = [GRADLE_BUILD_TIME : BUILD_TIME]
        }
        debug {
            manifestPlaceholders = [GRADLE_BUILD_TIME : BUILD_TIME]
        }
    }


    productFlavors.all {flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    productFlavors {
        dev{}
//        xiaomi {}
//        _360 {}
//        baidu {}
//        wandoujia {}
    }

    // 指定apk名
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            // 打包类型
            def buildTypeName = variant.buildType.name
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                // 打包名称
                def flavorName = variant.productFlavors[0].name
                // 版本名称
                def versionName = defaultConfig.versionName
                // haofang_release_baidu_ver1.0.0_build20150721.apk
                def fileName = "${PRODUCT_NAME}_${buildTypeName}_${flavorName}_ver${versionName}_build${BUILD_TIME_FORMAT}.apk"

                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    apt "org.androidannotations:androidannotations:$AAVersion"
    compile "org.androidannotations:androidannotations-api:$AAVersion"
    testCompile 'junit:junit:4.12'
    compile project(':ApiSdk')
    compile project(':publicutils')
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.jayway.android.robotium:robotium-solo:5.5.4'
    compile 'com.android.support:design:23.1.1'
}
apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName "com.joyfulmath.appframe"
    }
}



